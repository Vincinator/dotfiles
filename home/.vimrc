" ============================================================================
" Basic settings
" ============================================================================
set showmatch           
set number             
set expandtab           " Insert spaces when TAB is pressed.
set tabstop=4          
set shiftwidth=4        
let mapleader=" "       " Mapleader is space
set csprg=/usr/bin/cscope
" ============================================================================
" Plugins
" ============================================================================
call plug#begin('~/.vim/plugged')
" ----------------------------------------------------------------------------
" UI
" ----------------------------------------------------------------------------
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'ap/vim-buftabline'

" ----------------------------------------------------------------------------
" C Development
" ----------------------------------------------------------------------------

call plug#end()

" ============================================================================
" Plugin Config
" ============================================================================


" ----------------------------------------------------------------------------
" FZF
" ----------------------------------------------------------------------------
" default search command includes dotfiles but ignors .git folder
let $FZF_DEFAULT_COMMAND = 'ag --hidden --ignore .git -l -g ""'

" Default config. 
let g:fzf_action = {
  \ 'ctrl-t': 'tab split',
  \ 'ctrl-x': 'split',
  \ 'ctrl-v': 'vsplit' }

" height of fzf window 
let g:fzf_layout = { 'down': '~40%' }


" ============================================================================
" Mappings
" ============================================================================

" ----------------------------------------------------------------------------
" C Development
" ----------------------------------------------------------------------------

" Jump to Definition and back
nnoremap <silent><leader>j <C-]>zt
nnoremap <silent><leader>o <C-o>


" \             '--color', 'fg:188,fg+:222,bg+:#3a3a3a,hl+:104'],
function! Cscope(option, query)
  let color = '{ x = $1; $1 = ""; z = $3; $3 = ""; printf "\033[34m%s\033[0m:\033[31m%s\033[0m\011\033[37m%s\033[0m\n", x,z,$0; }'
  let opts = {
  \ 'source':  "cscope -dL" . a:option . " " . a:query . " | awk '" . color . "'",
  \ 'options': ['--ansi', '--prompt', '> ',
  \             '--multi', '--bind', 'alt-a:select-all,alt-d:deselect-all'],
  \ 'down': '40%'
  \ }
  function! opts.sink(lines) 
    let data = split(a:lines)
    let file = split(data[0], ":")
    execute 'e ' . '+' . file[1] . ' ' . file[0]
  endfunction
  call fzf#run(opts)
endfunction

function! CscopeQuery(option)
  call inputsave()
  if a:option == '0'
    let query = input('Assignments to: ')
  elseif a:option == '1'
    let query = input('Functions calling: ')
  elseif a:option == '2'
    let query = input('Functions called by: ')
  elseif a:option == '3'
    let query = input('Egrep: ')
  elseif a:option == '4'
    let query = input('File: ')
  elseif a:option == '6'
    let query = input('Definition: ')
  elseif a:option == '7'
    let query = input('Files #including: ')
  elseif a:option == '8'
    let query = input('C Symbol: ')
  elseif a:option == '9'
    let query = input('Text: ')
  else
    echo "Invalid option!"
    return
  endif
  call inputrestore()
  if query != ""
    call Cscope(a:option, query)
  else
    echom "Cancelled Search!"
  endif
endfunction


" ----------------------------------------------------------------------------
" General Navigation
" ----------------------------------------------------------------------------
" Switch between tabs
nmap <leader>1 <Plug>BufTabLine.Go(1)
nmap <leader>2 <Plug>BufTabLine.Go(2)
nmap <leader>3 <Plug>BufTabLine.Go(3)
nmap <leader>4 <Plug>BufTabLine.Go(4)
nmap <leader>5 <Plug>BufTabLine.Go(5)
nmap <leader>6 <Plug>BufTabLine.Go(6)
nmap <leader>7 <Plug>BufTabLine.Go(7)
nmap <leader>8 <Plug>BufTabLine.Go(8)
nmap <leader>9 <Plug>BufTabLine.Go(9)
nmap <leader>0 <Plug>BufTabLine.Go(10)

nmap <Leader>f :GFiles<CR>
nmap <Leader>F :Files<CR>

nmap <Leader>b :Buffers<CR>
nmap <Leader>h :History<CR>

nmap <Leader>t :BTags<CR>
nmap <Leader>T :Tags<CR>

nmap <Leader>l :BLines<CR>
nmap <Leader>L :Lines<CR>
nmap <Leader>' :Marks<CR>

nnoremap <silent> <Leader>ag :Ag <C-R><C-W><CR>

" ----------------------------------------------------------------------------
" Cscope mappings with fzf
" ----------------------------------------------------------------------------
" Assignments to
nnoremap <silent> <Leader>ca :call Cscope('0', expand('<cword>'))<CR>
" Functions calling
nnoremap <silent> <Leader>cc :call Cscope('1', expand('<cword>'))<CR>
"Functions called by
nnoremap <silent> <Leader>cd :call Cscope('2', expand('<cword>'))<CR>
" Egrep
nnoremap <silent> <Leader>ce :call Cscope('3', expand('<cword>'))<CR>
" File
nnoremap <silent> <Leader>cf :call Cscope('4', expand('<cword>'))<CR>
" Definition
nnoremap <silent> <Leader>cg :call Cscope('6', expand('<cword>'))<CR>
" files #including
nnoremap <silent> <Leader>ci :call Cscope('7', expand('<cword>'))<CR>
" C Symbol
nnoremap <silent> <Leader>cs :call Cscope('8', expand('<cword>'))<CR>
" Text
nnoremap <silent> <Leader>ct :call Cscope('9', expand('<cword>'))<CR>

" ============================================================================
" Functions & Commands
" ============================================================================

" ----------------------------------------------------------------------------
" :CSBuild
" ----------------------------------------------------------------------------
function! s:build_cscope_db(...)
  let git_dir = system('git rev-parse --git-dir')
  let chdired = 0
  if !v:shell_error
    let chdired = 1
    execute 'cd' substitute(fnamemodify(git_dir, ':p:h'), ' ', '\\ ', 'g')
  endif

  let exts = empty(a:000) ?
    \ ['java', 'c', 'h', 'cc', 'hh', 'cpp', 'hpp'] : a:000

  let cmd = "find . " . join(map(exts, "\"-name '*.\" . v:val . \"'\""), ' -o ')
  let tmp = tempname()
  try
    echon 'Building cscope.files'
    call system(cmd.' | grep -v /test/ > '.tmp)
    echon ' - cscoped db'
    call system('cscope -b -q -i'.tmp)
    echon ' - complete!'
    call s:add_cscope_db()
  finally
    silent! call delete(tmp)
    if chdired
      cd -
    endif
  endtry
endfunction
command! CSBuild call s:build_cscope_db(<f-args>)


" ----------------------------------------------------------------------------
" FZF Commands
" ----------------------------------------------------------------------------
nnoremap <silent> <Leader>C :call fzf#run({
\   'source':
\     map(split(globpath(&rtp, "colors/*.vim"), "\n"),
\         "substitute(fnamemodify(v:val, ':t'), '\\..\\{-}$', '', '')"),
\   'sink':    'colo',
\   'options': '+m',
\   'left':    30
\ })<CR>

function! s:tags_sink(line)
  let parts = split(a:line, '\t\zs')
  let excmd = matchstr(parts[2:], '^.*\ze;"\t')
  execute 'silent e' parts[1][:-2]
  let [magic, &magic] = [&magic, 0]
  execute excmd
  let &magic = magic
endfunction

function! s:tags()
  if empty(tagfiles())
    echohl WarningMsg
    echom 'Preparing tags'
    echohl None
    call system('ctags -R')
  endif

  call fzf#run({
  \ 'source':  'cat '.join(map(tagfiles(), 'fnamemodify(v:val, ":S")')).
  \            '| grep -v -a ^!',
  \ 'options': '+m -d "\t" --with-nth 1,4.. -n 1 --tiebreak=index',
  \ 'down':    '40%',
  \ 'sink':    function('s:tags_sink')})
endfunction

command! Tags call s:tags()







